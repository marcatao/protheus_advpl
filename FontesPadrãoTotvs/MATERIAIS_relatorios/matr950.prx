#INCLUDE "Matr950.ch"

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³ MATR950  ³ Autor ³     Marcos Simidu     ³ Data ³ 11.11.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Relatorio para conferencia diaria da DIPI.                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
FUNCTION MATR950
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define Variaveis ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
LOCAL aSv:=GetArea()
wnRel:="MATR950"
Titulo:="DIPI"
cDesc1:=STR0001 //"Emiss„o de listagem para para conferencia da DIPI."
cDesc2:=""
cDesc3:=STR0002 //"Ir  imprimir os lan‡amentos fiscais conforme os parƒmetros informados."
aReturn:={ STR0003, 1,STR0004, 2, 2, 1, "",1 } //"Zebrado"###"Administra‡„o"
nomeprog:="MATR950"
cPerg:="MTR950"
cString:="SF3"
Tamanho:="M"
Limite:=132
nTipo:=18
cabec1:=""
cabec2:=""
cabec3:=""
m_pag:=1
cbtxt:=SPACE(10)
cbcont:=0
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Envia controle para a funcao SETPRINT ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
nLastKey:=0
wnrel:=SetPrint(cString,wnrel,cPerg,Titulo,cDesc1,cDesc2,cDesc3,.F.,"",.F.,"G",,.F.)
If nLastKey==27
	dbClearFilter()
	Return
Endif
SetDefault(aReturn,cString)
If nLastKey==27
	dbClearFilter()
	Return
Endif
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Executa relatorio                                            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
#IFNDEF WINDOWS
	R950Imp(.F.,wnRel,cString,Tamanho)
	Set Device to Screen
#ELSE
	RptStatus({|lEnd| R950Imp(@lEnd,wnRel,cString,Tamanho)},titulo)
#ENDIF

If aReturn[5]==1
	Set Printer To
	ourspool(wnrel)
Endif

MS_FLUSH()
RestArea(aSv)
Return
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ R950Imp  ³ Autor ³    Marcos Simidu      ³ Data ³ 11.11.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Chamada da funcao de impressao                             ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
STATIC FUNCTION R950Imp(lEnd,wnRel,cString,Tamanho)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define variaveis              ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
LOCAL cInd1,cInd2,cInd3,cKey
LOCAL nRegua:=1

PRIVATE nLin:=80,nLargMax:=132
PRIVATE dDtIni,dDtFim
PRIVATE nAno,cNrLivro
PRIVATE cCfo_Ent,cCfo_Sai
PRIVATE cArqTemp
PRIVATE nPagina:=0
PRIVATE lAbortPrint:=.F.
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Chama grupo de perguntas.     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Pergunte(cPerg,.F.)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Grupo de perguntas                               ³
//³ mv_par01 - Data de.                              ³
//³ mv_par02 - Data ate.                             ³
//³ mv_par03 - Nro Livro.                            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Recebe valores dos Parametros ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dDtIni	:=mv_par01
dDtFim	:=mv_par02
cNrLivro	:=mv_par03
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Carrega ponto de entrada              ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cCFOEnt :="111/121/171/211/221/271/112/122/172/212/222/272/113/213/131/132/177/178/231/232/235/277/278/114/123/124/133/134/141/142/143/144/151/152/153/154/155/161/162/163/164/165/170/173/174/175/176/179/191/192/193/194/195/196/197/198/199/214/224/233/234/241/242/243/244/251/252/253/254/255/261/262/263/264/265/270/273/274/275/276/279/291/292/293/294/295/296/297/298/299"
cCFOSai :="511/514/516/521/525/571/572/611/614/616/618/621/625/671/672/512/515/517/522/526/573/574/612/615/617/619/622/626/673/674/513/613/531/532/577/578/631/632/635/677/678/523/524/533/534/541/542/543/544/545/551/552/553/561/562/563/570/575/576/579/591/592/593/594/595/596/597/599/623/624/633/634/641/642/643/644/645/651/652/653/661/662/663/670/675/676/679/691/692/693/694/695/696/697/699"
If (existblock("DIPIENT"))
	cCFO_Ent:=ExecBlock("DIPIENT",.F.,.F.)
Endif
If (existblock("DIPISAI"))
	cCFO_Sai:=ExecBlock("DIPISAI",.F.,.F.)
Endif
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Monta arquivo de trabalho.            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³  **** Estrutura do Arq. Trabalho ****   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
//	TIPOMOV   ,"C",01,0		"E"ntradas / "S"aidas
//	NOTA      ,"C",06,0		Numero da Nota
//	SERIE     ,"C",03,0		Serie
//	ITEM      ,"C",02,0		Item da Nota
//	TIPO      ,"C",01,0		Tipo da Nota
//	DT_ENTRADA,"D",08,0		Data de Entrada
//	DT_EMISSAO,"D",08,0		Data de Emissao
//	CLIFOR    ,"C",06,0		Cod do Cliente/Fornecedor
//	LOJA      ,"C",02,0		Loja
//	NOME      ,"C",20,0		Razao social
//	CGC       ,"C",18,0		CGC
//	UF        ,"C",02,0		UF
//	TIPOCLIFOR,"C",01,0		Tipo do Cliente/Fornecedor
//	PRODUTO   ,"C",15,0		Cod do Produto
//	DESCPROD  ,"C",50,0		Descricao do Produto
//	NBM       ,"C",12,0		Codigo NBM
//	TES       ,"C",03,0		TES
//	CFO       ,"C",03,0		CFO
//	VALMERC   ,"N",14,2		Valor da Mercadoria
//	VALIPI    ,"N",14,2		Valor do IPI
If (existblock("DIPIARQ"))
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Este ponto de entrada tera que retornar um arquivo de trabalho³
	//³ com a seguinte estrutura:                                   	³
	//³ "TIPOMOV"	,	"C"	,	1	,0 		// "E"ntradas / "S"aidas	³
	//³ "NOTA"   	,	"C"	,	6	,0			// Numero da Nota				³
	//³ "SERIE"		,	"C"	,	3	,0			// Serie							³
	//³ "ITEM"		,	"C"	,	2	,0			// Item da Nota				³
	//³ "TIPO"		,	"C"	,	1	,0			// Tipo da Nota				³
	//³ "DT_ENTRADA",	"D"	,	8	,0			// Data de Entrada			³
	//³ "DT_EMISSAO",	"D"	,	8	,0			// Data de Emissao			³
	//³ "CLIFOR"	,	"C"	,	6	,0			// Cod do Cliente/Forn.		³
	//³ "LOJA"		,	"C"	,	2	,0			// Loja							³
	//³ "NOME"		,	"C"	,	50	,0			// Razao social				³
	//³ "CGC"		,	"C"	,	18	,0			// CGC							³
	//³ "UF"			,	"C"	,	2	,0			// UF								³
	//³ "TIPOCLIFOR",	"C"	,	1	,0			// Tipo do Cliente/Forn.	³
	//³ "PRODUTO"	,	"C"	,	15	,0			// Cod do Produto				³
	//³ "DESCPROD"	,	"C"	,	50	,0			// Descricao do Produto		³
	//³ "NBM"		,	"C"	,	12	,0			// Codigo NBM					³
	//³ "TES"		,	"C"	,	3	,0			// TES							³
	//³ "CFO"		,	"C"	,	3	,0			// CFO							³
	//³ "VALMERC"	,	"N"	,	14	,2			// Valor da Mercadoria		³
	//³ "VALIPI"	,	"N"	,	14	,2			// Valor do IPI				³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

	cArqTemp	:= ExecBlock("DIPIARQ",.F.,.F.)
Else
	cArqTemp	:=	A975Moviment()
EndIf	

cInd1:=Substr(cArqTemp,1,6)+"1"	// Ordem por fornecedor
cInd2:=Substr(cArqTemp,1,6)+"2"	// Ordem por NBM
cInd3:=Substr(cArqTemp,1,6)+"3"	// Ordem por CFO

cKey:="TIPOMOV+CLIFOR+LOJA+TIPO+SERIE+NOTA"
IndRegua(cArqTemp,cInd1,cKey,,,STR0005) //"Selecionando Registros ... "
cKey:="NBM+TIPOMOV+SERIE+NOTA"
IndRegua(cArqTemp,cInd2,cKey,,,STR0005) //"Selecionando Registros ... "
cKey:="TIPOMOV+CFO+SERIE+NOTA"
IndRegua(cArqTemp,cInd3,cKey,,,STR0005) //"Selecionando Registros ... "

dbClearIndex()

dbSetIndex(cInd1+OrdBagExt())
dbSetIndex(cInd2+OrdBagExt())
dbSetIndex(cInd3+OrdBagExt())

dbSetOrder(1)
dbGotop()
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³  Inicializa variaveis da regua de processamento     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
nRegua:=Reccount()
SetRegua(nRegua)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³  Impressao por Cliente / Fornecedor                 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
R950CliFor()
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³  Inicializa variaveis da regua de processamento     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
SetRegua(nRegua)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³  Impressao por NBM.                                 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
R950NBM()
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³  Inicializa variaveis da regua de processamento     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
SetRegua(nRegua)

dbSelectArea(cArqTemp)
dbCloseArea()
Ferase(cInd1+OrdBagExt())
Ferase(cInd2+OrdBagExt())
Ferase(cInd3+OrdBagExt())
Ferase(cArqTemp+GetDBExtension())

Return
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³R950CliFor³ Autor ³     Marcos Simidu     ³ Data ³ 11.11.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Impressao de relacao por cliente/fornecedor.                ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function R950CliFor()
Local aDados,cTipoMov:="E",aL
Local aACumula:={0,0,0,0,0,0}  //[1][2]CliFor//[3][4]E/S//[5][6]Total Geral
Local cCliFor
Local lImpCab:=.F.
Local lFirst:=.F.
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³  Lay-Out de impressao do relatorio.                 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aL:=R950LayOut(1)

While !Eof()
	IncRegua()
	If Interrupcao(@lAbortPrint)
		Exit
	Endif

	aAcumula[3]:=0
	aAcumula[4]:=0

	lImpCab:=(cTipoMov!=TIPOMOV)
	cTipoMov:=TIPOMOV

	While !Eof().And.cTipoMov==TIPOMOV

		cCliFor:=CLIFOR+LOJA+TIPO
		lFirst:=.F.

		aAcumula[1]:=0
		aAcumula[2]:=0

		While !Eof().And.cTipoMov+cCliFor==TIPOMOV+CLIFOR+LOJA+TIPO
			IncRegua()
			If Interrupcao(@lAbortPrint)
				Exit
			Endif

			If nLin>55
				R950Cabec()
				If TIPOMOV=="E"
					FmtLin(,{aL[01],aL[02],aL[03],aL[04],aL[05],aL[06]},,,@nLin)
				Else
					FmtLin(,{aL[01],aL[11],aL[12],aL[13],aL[14],aL[15]},,,@nLin)
				Endif
			Else
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³  Flag para impressao de cabecalho do relatorio.     ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If lImpCab
					lImpCab:=.F.
					If TIPOMOV=="E"
						FmtLin(,{aL[01],aL[02],aL[03],aL[04],aL[05],aL[06]},,,@nLin)
					Else
						FmtLin(,{aL[01],aL[11],aL[12],aL[13],aL[14],aL[15]},,,@nLin)
					Endif
				Endif
			Endif

			aDados:=Array(10)

			aDados[01]:=CLIFOR
			aDados[02]:=LOJA
			aDados[03]:=Padr(NOME,28)
			If TIPOMOV=="E"
				aDados[04]:=DT_ENTRADA
			Else
				aDados[04]:=DT_EMISSAO
			Endif
			aDados[05]:=SERIE
			aDados[06]:=NOTA
			aDados[07]:=TIPO
			aDados[08]:=CFO
			aDados[09]:=VALMERC
			aDados[10]:=VALIPI

			If lFirst
				If TIPOMOV=="E"
					FmtLin({aDados[4],aDados[5],aDados[6],aDados[7],aDados[8],aDados[9],aDados[10]},aL[08],,,@nLin)
				Else
					FmtLin({aDados[4],aDados[5],aDados[6],aDados[7],aDados[8],aDados[9],aDados[10]},aL[17],,,@nLin)
				Endif
			Else
				lFirst:=.T.
				If TIPOMOV=="E"
					FmtLin(@aDados,aL[07],,,@nLin)
				Else
					FmtLin(@aDados,aL[16],,,@nLin)
				Endif
			Endif
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³  Acumula valores totais da NF e do IPI.             ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			aAcumula[1]+=VALMERC		// Total cli/fornec.
			aAcumula[3]+=VALMERC		// Total Entradas
			aAcumula[5]+=VALMERC		// Total geral mercadorias.

			aAcumula[2]+=VALIPI		// Total do IPI cli/fornec.
			aAcumula[4]+=VALIPI		// Total IPI das Saidas
			aAcumula[6]+=VALIPI		// Total geral do IPI.

			dbSkip()
		EndDo
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³  Impressao de totais do Cli/Fornec.                 ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If (aAcumula[1]+aAcumula[2])>0
			FmtLin({aAcumula[1],aAcumula[2]},aL[10],,,@nLin)
			FmtLin(,aL[09],,,@nLin)
		Endif
	Enddo
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³  Impressao de totais tipo de movimento.             ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If (aAcumula[3]+aAcumula[4])>0
		If cTipoMov=="E"
			FmtLin({aAcumula[3],aAcumula[4]},aL[20],,,@nLin)
		Else
			FmtLin({aAcumula[3],aAcumula[4]},aL[21],,,@nLin)
		Endif
		FmtLin(,aL[09],,,@nLin)
	Endif
EndDo
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³  Impressao de totais do Cli/Fornec.                 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If (aAcumula[5]+aAcumula[6])>0
	FmtLin({aAcumula[5],aAcumula[6]},aL[19],,,@nLin)
	FmtLin(,aL[09],,,@nLin)
Endif

Return
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³  R950NBM ³ Autor ³     Marcos Simidu     ³ Data ³ 11.11.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Impressao de relacao por NBM.                               ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function R950NBM()
Local aDados,cTipoMov:="E",aL
Local aACumula:={0,0,0,0,0,0}  //[1][2]NBM //[3][4]E/S //[5][6] Geral
Local cProduto
Local lFirst:=.F.

nLin:=80

dbSelectArea(cArqTemp)
dbSetOrder(2)
dbGotop()
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³  Lay-Out de impressao do relatorio.                 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aL:=R950LayOut(2)

While !Eof()
	IncRegua()
	If Interrupcao(@lAbortPrint)
		Exit
	Endif

	aAcumula[3]:=0
	aAcumula[4]:=0
	cProduto:=NBM

	While !Eof().And.cProduto==NBM

		cTipoMov:=TIPOMOV
		lFirst:=.F.

		aAcumula[1]:=0
		aAcumula[2]:=0

		While !Eof().And.cTipoMov+cProduto==TIPOMOV+NBM
			IncRegua()
			If Interrupcao(@lAbortPrint)
				Exit
			Endif

			If nLin>55
				R950Cabec()
				FmtLin(,{aL[01],aL[02],aL[03],aL[04],aL[05],aL[06]},,,@nLin)
			Endif

			aDados:=Array(6)

			aDados[01]:=NBM
			aDados[02]:=PRODUTO
			aDados[03]:=Padr(DESCPROD,28)
			If TIPOMOV=="E"
				aDados[04]:=DT_ENTRADA
			Else
				aDados[04]:=DT_EMISSAO
			Endif
			aDados[05]:=VALMERC
			aDados[06]:=VALIPI

			If lFirst
				FmtLin({aDados[2],aDados[3],aDados[4],aDados[5],aDados[6]},aL[10],,,@nLin)
			Else
				FmtLin(@aDados,aL[07],,,@nLin)
				lFirst:=.T.
			Endif
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³  Acumula valores totais da NF e do IPI.             ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If cTipoMov=="E"
				aAcumula[3]+=VALMERC		// Total NBM
				aAcumula[4]+=VALIPI		// Total IPI
			Else
				aAcumula[3]-=VALMERC		// Total NBM
				aAcumula[4]-=VALIPI		// Total IPI
			Endif

			aAcumula[1]+=VALMERC		// Total E/S NBM
			aAcumula[2]+=VALIPI		// Total E/S IPI

			If cTipoMov=="E"
				aAcumula[5]+=VALMERC		// Total Geral NBM
				aAcumula[6]+=VALIPI		// Total Geral IPI
			Else
				aAcumula[5]-=VALMERC		// Total Geral NBM
				aAcumula[6]-=VALIPI		// Total Geral IPI
			Endif

			dbSkip()
		EndDo
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³  Impressao de totais tipo de movimento.             ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If cTipoMov!=TIPOMOV.And.(aAcumula[1]+aAcumula[2])>0
			If cTipoMov=="E"
				FmtLin({aAcumula[1],aAcumula[2]},aL[12],,,@nLin)
			Else
				FmtLin({aAcumula[1],aAcumula[2]},aL[13],,,@nLin)
			Endif
			FmtLin(,aL[08],,,@nLin)
		Endif
	Enddo
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³  Impressao de saldos.                               ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	FmtLin({cProduto,aAcumula[3],aAcumula[4]},aL[9],,,@nLin)
	FmtLin(,aL[08],,,@nLin)
EndDo
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³  Impressao de totais do Cli/Fornec.                 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If (aAcumula[5]+aAcumula[6])<>0
	FmtLin({aAcumula[5],aAcumula[6]},aL[11],,,@nLin)
	FmtLin(,aL[08],,,@nLin)
Endif

Return
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³R950LayOut³ Autor ³     Marcos Simidu     ³ Data ³ 11.11.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³LayOut do relatorio.                                        ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function R950LayOut(nLayOut)
Local aL

Do Case
	Case nLayOut==1
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³  LayOut dos Remetentes.                             ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		aL:=Array(21)
		aL[01]:=" "
		aL[02]:=Padc(STR0006,132) //"DADOS DOS REMETENTES"
		aL[03]:=Padc("====================",132)
		aL[04]:=" "
		aL[05]:=Padc(STR0007,132) //"CODIGO LOJA            REMETENTE              DATA    SERIE NUMERO TIPO CFO  VALOR MERCADORIA     VALOR DO IPI   "
		aL[06]:=Padc("====== ==== ============================== ========== ===== ====== ==== === ================== ==================",132)
		aL[07]:=Padc("######  ##  ############################## ##########  ###  ######  ##  ### ################## ##################",132)
		aL[08]:=Padc("                                           ##########  ###  ######  ##  ### ################## ##################",132)
		aL[09]:=Padc("-----------------------------------------------------------------------------------------------------------------",132)
		aL[10]:=Padc(STR0008,132) //"TOTAL                                                                       ################## ##################"

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³  LayOut dos Destinatarios.                          ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		aL[11]:=Padc(STR0009,132) //"DADOS DOS DESTINATARIOS"
		aL[12]:=Padc("=======================",132)
		aL[13]:=" "
		aL[14]:=Padc(STR0010,132) //"CODIGO LOJA          DESTINATARIO             DATA    SERIE NUMERO TIPO CFO   VALOR CONTABIL      VALOR DO IPI   "
		aL[15]:=Padc("====== ==== ============================== ========== ===== ====== ==== === ================== ==================",132)
		aL[16]:=Padc("######  ##  ############################## ##########  ###  ######  ##  ### ################## ##################",132)
		aL[17]:=Padc("                                           ##########  ###  ######  ##  ### ################## ##################",132)
		aL[18]:=Padc("-----------------------------------------------------------------------------------------------------------------",132)
		aL[19]:=Padc(STR0011,132) //"TOTAL GERAL                                                                 ################## ##################"
		aL[20]:=Padc(STR0012,132) //"TOTAL ENTRADAS                                                              ################## ##################"
		aL[21]:=Padc(STR0013,132) //"TOTAL SAIDAS                                                                ################## ##################"

	Case nLayOut==2
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³  LayOut dos produtos por NBM.                       ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		aL:=Array(13)
		aL[01]:=" "
		aL[02]:=Padc(STR0014,132) //"DADOS DOS PRODUTOS - POR NBM"
		aL[03]:=Padc("============================",132)
		aL[04]:=" "
		aL[05]:=Padc(STR0015,132) //"CODIGO NBM/SH CODIGO DO PRODUTO       MERCADORIAS/INSUMOS         DATA           VALOR          VALOR DO IPI   "
		aL[06]:=Padc("============= ================= ============================== ========== ================== ==================",132)
		aL[07]:=Padc("############# ################  ############################## ########## ################## ##################",132)
		aL[08]:=Padc("---------------------------------------------------------------------------------------------------------------",132)
		aL[09]:=Padc(STR0016,132) //"SALDO DA NBM  ################                                            ################## ##################"
		aL[10]:=Padc("              ################  ############################## ########## ################## ##################",132)
		aL[11]:=Padc(STR0017,132) //"TOTAL GERAL                                                               ################## ##################"
		aL[12]:=Padc(STR0018,132) //"TOTAL ENTRADA                                                             ################## ##################"
		aL[13]:=Padc(STR0019,132) //"TOTAL SAIDAS                                                              ################## ##################"
	Case nLayOut==3
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³  LayOut do cabecalho do relatorio.                  ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		aL:=Array(8)
		aL[01]:=Padc(STR0020,132) //"LISTAGEM DE OPERACOES - CONFERENCIA DA DIPI"
		aL[02]:=" "
		aL[03]:=STR0021 //"EMPRESA #####################################################                                                      PAGINA #######"
		aL[04]:=" "
		aL[05]:=STR0022 //"ENDERECO ############################################ CNPJ   #####################             PERIODO DE ########## A ##########"
		aL[06]:=" "
		aL[07]:=STR0023 //"CIDADE #################################### UF ## CEP ######### INSCRICAO ESTADUAL ################          EMISSAO ############"
		aL[08]:="---------------------------------------------------------------------------------------------------------------------------------"
EndCase

Return(aL)
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³ R950Cabec³ Autor ³     Marcos Simidu     ³ Data ³ 11.11.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Imprime cabecalho do relatorio.                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function R950Cabec()
Local aL

aL:=R950LayOut(3)

nPagina++	// Incrementa pagina do relatorio
cPagina:=StrZero(nPagina,7)
nLin:=0
@nLin,00 PSAY AvalImp(132)
nLin++

FmtLin(,{aL[1],aL[2]},,,@nLin)
FmtLin({SM0->M0_NOMECOM,cPagina},aL[3],,,@nLin)
FmtLin(,aL[4],,,@nLin)
FmtLin({SM0->M0_ENDENT,SM0->M0_CGC,dToc(dDtIni),dToc(dDtFim)},aL[5],,,@nLin)
FmtLin(,aL[6],,,@nLin)
FmtLin({SM0->M0_CIDENT,SM0->M0_ESTENT,Transform(SM0->M0_CEPENT,"@R 99999-999"),InscrEst(),dToc(dDataBase)},aL[7],,,@nLin)
FmtLin(,aL[8],,,@nLin)

Return(NIL)
